
EventPointerTable(AssetCh11Data, ChapterInfo)

#ifndef SetSymbolDefined
  #define SetSymbolDefined
  #define SetSymbol(name, val) "PUSH; ORG (val); name :; POP"
#endif

SetSymbol(cntKillCount, 0)
SetSymbol(cntReinCount, 1)
SetSymbol(cntReinNumLo, 2)
SetSymbol(cntReinNumHi, 3)

SetSymbol(flagEnemyDeath, 10)

ALIGN 4
ChapterInfo:
    POIN TurnEvents
    POIN TalkEvents
    POIN TileEvents
    POIN ActionEvents
    POIN NoEvents NoEvents NoEvents
    POIN NoTutorials
    POIN NoTraps NoTraps
    POIN unDeployList unDeployList
    POIN 0 0 0 0 0 0
    POIN scBeginning scEnding

ALIGN 4
NoEvents:
    END_MAIN

ALIGN 4
NoTutorials:
    WORD 0

ALIGN 4
NoTraps:
    ENDTRAP

ALIGN 4
TurnEvents:
    TURN 3 scEnding [9] 0
    TURN 0 scCheckReinf [1, 0xFF] 0
    END_MAIN

ALIGN 4
TalkEvents:
    END_MAIN

ALIGN 4
TileEvents:
    END_MAIN

ALIGN 4
ActionEvents:
    AFEV 0 scOnEnemyDeath flagEnemyDeath
    END_MAIN

ALIGN 4
scIncKillCount:
{
    // increments cntKillCount
    // if cntKillCount reaches 4, resets it and increments cntReinCount

    SetSymbol(end, 0)

    // increment kill count
    COUNTER_INC cntKillCount

    // compare kill count to 4, end if it's lower
    SVAL s1 4
    COUNTER_CHECK cntKillCount
    BLT end sC s1

    // reset kill count and increase reinf count
    COUNTER_SET cntKillCount 0
    COUNTER_INC cntReinCount

LABEL end
    ENDA
}

ALIGN 4
scResetCounters:
{
    // resets all of this chapters counters to their initial value

    COUNTER_SET cntKillCount 0
    COUNTER_SET cntReinCount 0
    COUNTER_SET cntReinNumLo 0
    COUNTER_SET cntReinNumHi 0

    ENDA
}

ALIGN 4
scGetNextReinNum:
{
    // increments cntReinNumLo:cntReinNumHi
    // ou: sC = reinforcement number (= current value of cntReinNumLo:cntReinNumHi)

    SetSymbol(wrap,   0)
    SetSymbol(return, 1)

    // if cntReinNumLo reaches max (15), reset it and increment cntReinNumHi
    COUNTER_CHECK cntReinNumLo
    SVAL s1 15
    BEQ wrap sC s1

    COUNTER_INC cntReinNumLo

LABEL return
    // s1 = cntReinNumHi << 4
    COUNTER_CHECK cntReinNumHi
    SVAL s1 4
    SLSL s1 sC s1

    // sC = cntReinNumLo + (cntReinNumHi << 4)
    COUNTER_CHECK cntReinNumLo
    SADD sC sC s1

    ENDA

LABEL wrap
    COUNTER_SET cntReinNumLo 0
    COUNTER_INC cntReinNumHi

    GOTO return
}

ALIGN 4
scOnEnemyDeath:
{
    // scene attached to enemy death flag
    // increments kill count

    // allow the event to be run again, when another enemy dies
    FLAG_F flagEnemyDeath

    CALL scIncKillCount

    NoFade
    ENDA
}

ALIGN 4
scCheckReinf:
{
    // scene attached to start of turn
    // loads any queued reinforcements

    SetSymbol(lop, 0)
    SetSymbol(end, 1)

LABEL lop
    COUNTER_CHECK cntReinCount

    BEQ end sC s0

    COUNTER_DEC cntReinCount

    CALL scGetNextReinNum

#ifdef __DEBUG__
    SADD s3 sC s0
    GIVEMONEY Gunborg
#endif

    SADD s2 sC s0
    CALL scLoadReinforcements

    GOTO lop

LABEL end
    NoFade
    ENDA
}

ALIGN 4
scBeginning:
{
    // beginning scene

    CALL scResetCounters

#ifdef __DEBUG__
    // load in debug party
    LOAD 1 unDeployList
    ENUN
#endif

    GotoPrepScreen
    ENDA
}

ALIGN 4
scEnding:
{
    // ending scene

    // set flag 3 just in case
    // if this isn't set at the end of a chapter, turn count and beat time won't be registered
    FLAG_T 3

    GOTO_CHAPTER_NOWM 11

    ENDA
}

ALIGN 4
unDeployList:
    UNIT Gunborg  GunborgGreatLord Level(5,  Ally, True) [4,  16] 0 [SilverSword, Elixir]
    UNIT Bastion  GreatKnight      Level(5,  Ally, True) [5,  16] 0 [SilverAxe, SilverLance, Elixir]
    UNIT Aldric   General          Level(5,  Ally, True) [4,  17] 0 [SilverLance, SilverAxe, SilverSword, Elixir]
    UNIT Scully   Warrior          Level(5,  Ally, True) [16, 2]  0 [SilverAxe, SilverBow, Elixir]
    UNIT Wilbur   Bishop           Level(5,  Ally, True) [17, 2]  0 [Aura, Mend, Elixir]
    UNIT Paula    Swordmaster_F    Level(5,  Ally, True) [16, 3]  0 [SilverSword, Shamshir, Runesword, Elixir]
    UNIT Serahlta Rogue            Level(5,  Ally, True) [3,  2]  0 [SilverSword]
    UNIT Yin      FalcoKnight      Level(5,  Ally, True) [2,  2]  0 [SilverSword, SilverLance, Spear, Elixir]
    UNIT Yang     WyvernKnight_F   Level(5,  Ally, True) [3,  1]  0 [SilverLance, Spear, Elixir]
    UNIT Calvin   Cyclops          Level(15, Ally, True) [16, 12] 0 [SilverAxe, Tomahawk, Elixir]
    UNIT Hobbes   ArchMogall       Level(5,  Ally, True) [16, 13] 0 [EvilEye, CrimsonEye, Elixir]
    UNIT Yara     Summoner         Level(5,  Ally, True) [17, 12] 0 [Nosferatu, Nosferatu, Elixir]
    UNIT
